plugins {
    id "application"
    id "idea"
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    modularity.inferModulePath = true
}
def mainClassName = "org.${project.name}.cli.Main"
application {
    mainModule.set("${project.name}")
    mainClass.set(mainClassName)
    applicationDefaultJvmArgs = [
            // the cli is short lived, so let's enable a fast JVM startup
            "-XX:TieredStopAtLevel=1"
    ]
}

processResources {
    filesMatching("**/application.properties") {
        expand(version: project.version)
    }
}
compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}
test {
    useJUnitPlatform()
}
repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/integration-test/java")
        }
        resources.srcDir file("src/integration-test/resources")
    }
}

project.idea.module {
    // Mark integration tests are test sources for Intellij IDEA
    sourceDirs -= project.sourceSets.integrationTest.java.srcDirs
    sourceDirs -= project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    def lombokVersion = "1.18.16"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    integrationTestCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    integrationTestAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    def picocliVersion = "4.6.1"
    implementation "info.picocli:picocli:${picocliVersion}"
    annotationProcessor "info.picocli:picocli-codegen:${picocliVersion}"
    // testing
    def junitVersion = "5.5.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.14.0"
    def mockitoVersion = "3.6.28"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

    integrationTestImplementation sourceSets.main.output
    integrationTestImplementation sourceSets.test.output
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "build/libs"
}
task integrationTest(type: Test) {
    dependsOn copyDependencies, jar
    useJUnitPlatform()
    description = "Runs integration tests."
    group = "verification"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn integrationTest

task buildNativePackage(type: Exec) {
    dependsOn installDist
    workingDir "${buildDir}"

    commandLine "jpackage",
            "--name",
            "${project.name}",
            "--app-version",
            "${project.version}",
            "--module-path",
            "install/${project.name}/lib",
            "-m",
            "${project.name}/${mainClassName}",
            "--license-file",
            "../LICENSE",
            "--dest",
            "."
}

task buildNativeImage(type: Exec) {
    dependsOn installDist
    inputs.dir("${buildDir}/install/${project.name}/lib/")
    outputs.file("${buildDir}/libs/${project.name}")
    workingDir "${buildDir}/libs"

    commandLine nativeImageBin(),
            "--verbose",
            "--no-fallback",
            "-H:Name=${project.name}",
            "-H:-AddAllCharsets",
            "-H:-UseServiceLoaderFeature",
            "-J-Drx.unsafe-disable=true",
            "--initialize-at-build-time",
            "-DremoveUnusedAutoconfig=true",
            "--report-unsupported-elements-at-runtime", // reflection features not used in native-image mode
            "-cp", "${buildDir}/install/${project.name}/lib/*",
            "${mainClassName}"
}

private static String nativeImageBin() {
    def env = System.env.GRAALVM_HOME ?: System.env.JAVA_HOME
    if (env != null) {
        return "${env}/bin/native-image"
    }
    return "native-image"
}

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn installDist
    inputs.dir("${buildDir}/install/${project.name}/")
    outputs.dir("${buildDir}/docs/manpage-adoc")
    classpath "${buildDir}/install/${project.name}/lib/*", configurations.annotationProcessor
    main "picocli.codegen.docgen.manpage.ManPageGenerator"
    args "org.symly.cli.MainCommand",
            "-c",
            "org.symly.cli.BeanFactory",
            "--outdir=${buildDir}/docs/manpage-adoc"
}

asciidoctor {
    dependsOn generateManpageAsciiDoc
    sourceDir = file("${buildDir}/docs/manpage-adoc")
    outputDir = file("${buildDir}/docs/manpage")
    logDocuments = true
    outputOptions {
        backends = ["manpage", "html5"]
    }
}

task generateManpage(type: Exec) {
    dependsOn asciidoctor
    inputs.dir("${buildDir}/docs/manpage/manpage")
    outputs.dir("${buildDir}/docs/manpage/gz")
    workingDir "${buildDir}/docs"
    commandLine "/bin/sh", "-c", "rm -rf manpage/gz && cp -R manpage/manpage manpage/gz && gzip -9 manpage/gz/*"
}

task generateShellCompletions(type: JavaExec) {
    dependsOn installDist
    inputs.dir("${buildDir}/install/${project.name}/")
    outputs.file("${buildDir}/shell/completions/${project.name}")
    classpath "${buildDir}/install/${project.name}/lib/*"
    main "picocli.AutoComplete"
    args "org.${project.name}.cli.MainCommand",
            "--factory=org.${project.name}.cli.BeanFactory",
            "--completionScript=${buildDir}/shell/completions/${project.name}"
}

// DEBIAN packaging
// needs a native image to be generated for a linux-amd64 target first 
task prepareDebianPackage(type: Copy) {
    dependsOn generateManpage, generateShellCompletions
    from("LICENSE") {
        into "usr/share/doc/${project.name}"
        rename "LICENSE", "copyright"
    }
    from("packaging/${project.name}") {
        expand(version: project.version)
    }
    from("${buildDir}/libs/${project.name}") {
        into "usr/local/bin/"
    }
    from("${buildDir}/docs/manpage/gz") {
        into "usr/man/man1"
        rename "LICENSE", "copyright"
    }
// requires https://github.com/remkop/picocli/issues/1346
//    from("${buildDir}/shell/completions") {
//        into "/usr/share/bash-completion/completions"
//    }
    into "${buildDir}/packaging/debian"
}

task buildDebianPackage(type: Exec) {
    dependsOn prepareDebianPackage
    inputs.dir("${buildDir}/packaging/debian")
    outputs.file("${buildDir}/packaging/${project.name}_${project.version}.deb")
    workingDir "${buildDir}/packaging"
    commandLine "dpkg", "--build", "debian", "."
}
