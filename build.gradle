plugins {
    id 'application'
    id 'idea'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    modularity.inferModulePath = true
}
def mainClassName = 'org.linky.cli.Main'
application {
    mainModule = 'linky'
    mainClass = mainClassName
    applicationDefaultJvmArgs = [
            // the cli is short lived, so let's enable a fast JVM startup
            '-XX:TieredStopAtLevel=1'
    ]
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}
test {
    useJUnitPlatform()
}
repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

project.idea.module {
    // Mark integration tests are test sources for Intellij IDEA
    sourceDirs -= project.sourceSets.integrationTest.java.srcDirs
    sourceDirs -= project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    def lombokVersion = "1.18.16"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    integrationTestCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    integrationTestAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    def picocliVersion = "4.5.2"
    implementation "info.picocli:picocli:${picocliVersion}"
    annotationProcessor "info.picocli:picocli-codegen:${picocliVersion}"
    // testing
    def junitVersion = "5.5.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.14.0"
    def mockitoVersion = '3.6.28'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

    integrationTestImplementation sourceSets.main.output
    integrationTestImplementation sourceSets.test.output
}


task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs'
}
task integrationTest(type: Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}
integrationTest.dependsOn copyDependencies
integrationTest.dependsOn jar
check.dependsOn integrationTest

task buildNativePackage(type: Exec) {
    workingDir "${buildDir}"

    commandLine "jpackage",
            "--name",
            "${project.name}",
            "--app-version",
            "${project.version}",
            "--module-path",
            "install/${project.name}/lib",
            "-m",
            "${project.name}/${mainClassName}",
            "--license-file",
            "../LICENSE",
            "--dest",
            "."
}
buildNativePackage.dependsOn installDist

task buildNativeImage(type: Exec) {
    workingDir "${buildDir}/libs"

    commandLine "native-image",
            "--verbose",
            "--no-fallback",
            "-H:Name=${project.name}",
            "-H:-AddAllCharsets",
            "-H:-UseServiceLoaderFeature",
            "-J-Drx.unsafe-disable=true",
            "-O1",
            "--no-server",
            "--initialize-at-build-time",
            "-DremoveUnusedAutoconfig=true",
            "--report-unsupported-elements-at-runtime", // reflection features not used in native-image mode
            "-cp", "${buildDir}/install/${project.name}/lib/*",
            "${mainClassName}"
}
buildNativeImage.dependsOn installDist


// DEBIAN packaging
// needs a native image to be generated for a linux-amd64 target first 
task prepareDebianPackage(type: Copy) {
    from("packaging/linky") {
        expand(version: project.version)
    }
    from("build/libs/linky") {
        into "usr/local/bin/"
    }
    into "${buildDir}/packaging/debian"
}

task buildDebianPackage(type:Exec) {
    workingDir "${buildDir}/packaging"
    commandLine "dpkg", "--build", "debian", "."
}
buildDebianPackage.dependsOn prepareDebianPackage
